# PubChemRDF Case 11: What are the top five diseases commonly mentioned with indomethacin (CID3715)?

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX compound: <http://rdf.ncbi.nlm.nih.gov/pubchem/compound/>

SELECT ?disease ?score ?disease_prefLabel
FROM <http://rdf.ncbi.nlm.nih.gov/pubchem/cooccurrence>
FROM <http://rdf.ncbi.nlm.nih.gov/pubchem/disease>
WHERE {
    ?cooccurrence rdf:subject compound:CID3715 .
    ?cooccurrence rdf:object ?disease .
    ?cooccurrence rdf:type sio:SIO_000993 .
    ?cooccurrence sio:SIO_000300 ?score .
    ?disease skos:prefLabel ?disease_prefLabel .
}
ORDER BY DESC(?score)
LIMIT 5