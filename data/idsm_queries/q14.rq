# PubChemRDF Case 14: What are the top ten diseases co-occurring with the gene most commonly mentioned with maribavir (CID471161)?

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX compound: <http://rdf.ncbi.nlm.nih.gov/pubchem/compound/>

SELECT ?gene ?disease ?disease_prefLabel
FROM <http://rdf.ncbi.nlm.nih.gov/pubchem/cooccurrence>
FROM <http://rdf.ncbi.nlm.nih.gov/pubchem/disease>
WHERE {
  {
    SELECT ?gene
    WHERE {
      ?cooccurrence1 rdf:subject compound:CID471161 .
      ?cooccurrence1 rdf:object ?gene .
      ?cooccurrence1 rdf:type sio:SIO_001257 .
      ?cooccurrence1 sio:SIO_000300 ?score1 .
    }
    ORDER BY DESC(?score1)
    LIMIT 1
  }
  ?cooccurrence2 rdf:subject ?gene .
  ?cooccurrence2 rdf:object ?disease .
  ?cooccurrence2 rdf:type sio:SIO_000983 .
  ?cooccurrence2 sio:SIO_000300 ?score2 .
  ?disease skos:prefLabel ?disease_prefLabel .
}
ORDER BY DESC(?score2)
LIMIT 10