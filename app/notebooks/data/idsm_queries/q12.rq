# PubChemRDF Case 12: What are the three most recent references that mention indomethacin (CID3715) and inflammation (DZID8173)?

PREFIX prism: <http://prismstandard.org/namespaces/basic/3.0/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX vocab: <http://rdf.ncbi.nlm.nih.gov/pubchem/vocabulary#>
PREFIX disease: <http://rdf.ncbi.nlm.nih.gov/pubchem/disease/>
PREFIX compound: <http://rdf.ncbi.nlm.nih.gov/pubchem/compound/>

SELECT ?ref ?date ?journal ?title
FROM <http://rdf.ncbi.nlm.nih.gov/pubchem/reference>
WHERE {
  ?ref vocab:discussesAsDerivedByTextMining compound:CID3715 .
  ?ref vocab:discussesAsDerivedByTextMining disease:DZID8173 .
  ?ref dcterms:date ?date .
  ?ref dcterms:title ?title .
  ?ref prism:publicationName ?journal .
}
ORDER BY DESC(?date)
LIMIT 3